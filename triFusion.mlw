(* why3 prove -P alt-ergo -t 60 triFusion.mlw 
Augmenter le timeout si nécessaire, mais ça marche*)


module TriFusion

  use int.Int
  use list.List
  use list.Length
  use list.Append
  use list.Permut
  use list.SortedInt
  use Listes
  

  val function split (l :list int) : (list int, list int)
    ensures { let (l1,l2) = result in length l < 2 || 
             (length l >= 2 && length l1 < length l && length l2 < length l) } 
    ensures { let (l1,l2) = result in permut l (l1 ++ l2) } 


  val function merge (l1 l2 :list int) : list int
    requires { sorted l1 /\ sorted l2  }
    ensures { sorted result } 
    ensures { permut (l1 ++ l2) result } 

  
  let rec function mergesort (l :list int)
    ensures { sorted result } 
    ensures { permut result l } 
    variant { length l }
  = match l with
    | Nil -> Nil
    | Cons x Nil -> Cons x Nil
    | _ ->   let (l1,l2) = split l
             in merge (mergesort l1) (mergesort l2)
    end

end
